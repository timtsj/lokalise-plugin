buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'distribution'

group 'com.github.timtsj'
version '1.5'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile(group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('artifacts'))
        }
    }
}

pluginBundle {
    website = 'https://timtsj.github.io/lokalise-plugin/'
    vcsUrl = 'https://github.com/timtsj/lokalise-plugin'
    description = 'This is a gradle plugin for android. Refresh (upload and download) localization files using lokalise.co'
    tags = ['android', 'lokalise-plugin', 'lokalise']

    plugins {
        LokalisePlugin {
            id = 'com.timtsj.lokalise'
            displayName = 'Gradle lokalise-plugin'
        }
    }

    mavenCoordinates {
        groupId = "com.github.timtsj"
        artifactId = "lokalise-plugin"
        version = "1.5"
    }
}

def distributionsCopySpec = copySpec {
    into("$name-$version") {
        into('libs') {
            from 'distribution'
            include '*.jar'
        }
        from 'README.md'
    }
}

task fatJarZip(type: Zip) { with distributionsCopySpec }
task fatJarTar(type: Tar) { with distributionsCopySpec }

uploadArchives.dependsOn(fatJarZip, fatJarTar)
